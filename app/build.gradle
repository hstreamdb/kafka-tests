plugins {
    id 'application'
    id "com.diffplug.spotless" version "6.21.0"
    id "java"
}

repositories {
    if (project.hasProperty("enableMavenLocal")) {
        project.logger.info("enabled mavenLocal")
        mavenLocal()
    }
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/apache-snapshots'
    }
    mavenCentral()
    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
}

def DEFAULT_CLIENT_VERSION = "3.5.1"
def KAFKA_CLIENT_VERSION = DEFAULT_CLIENT_VERSION
if (System.getenv("KAFKA_CLIENT_VERSION") != null) {
    KAFKA_CLIENT_VERSION = System.getenv("KAFKA_CLIENT_VERSION")
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation "org.testcontainers:testcontainers:1.19.0"
    testImplementation "org.testcontainers:junit-jupiter:1.19.0"
    testImplementation "org.testcontainers:toxiproxy:1.19.0"

    System.out.println("KAFKA_CLIENT_VERSION:" + KAFKA_CLIENT_VERSION)
    testImplementation "org.apache.kafka:kafka-clients:${KAFKA_CLIENT_VERSION}"
    // testImplementation 'org.apache.kafka:kafka-clients:0.11.0.3'
    // testImplementation 'org.apache.kafka:kafka-clients:1.0.0'
    //    testImplementation 'org.apache.kafka:kafka-clients:1.1.1'
    // testImplementation 'org.apache.kafka:kafka-clients:3.5.1'
    testImplementation 'com.github.docker-java:docker-java:3.3.3'

    // lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.0'

    testImplementation "org.assertj:assertj-core:3.24.2"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        googleJavaFormat()
    }

    groovyGradle {
        target '*.gradle'
        greclipse()
        indentWithSpaces()
    }
}

sourceSets {
    test {
        java {
            if (KAFKA_CLIENT_VERSION != DEFAULT_CLIENT_VERSION) {
                exclude 'io/hstream/kafka/testing/version_3_5/**'
            }
        }
    }
}
